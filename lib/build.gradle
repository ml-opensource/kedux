plugins {
    id 'org.jetbrains.kotlin.multiplatform'
    id 'org.jetbrains.kotlin.native.cocoapods'
}
group 'com.example'
version '0.0.1'

apply plugin: 'maven-publish'
apply plugin: 'com.android.library'

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
}


kotlin {
    jvm()
    android()
    js {
        browser {
        }
        nodejs {
        }
    }
    // For ARM, should be changed to iosArm32 or iosArm64
    // For Linux, should be changed to e.g. linuxX64
    // For MacOS, should be changed to e.g. macosX64
    // For Windows, should be changed to e.g. mingwX64
    macosX64("macos")
    ios("ios")
    cocoapods {
        // Configure fields required by CocoaPods.
        summary = "Kedux is a kotlin-multiplatform state management library."
        homepage = "Link to a Kotlin/Native module homepage"
    }
    [targets.iosX64, targets.iosArm64].forEach {
        it.binaries
                .findAll { it instanceof org.jetbrains.kotlin.gradle.plugin.mpp.Framework }
                .every {
                    it.baseName = "Kedux"
                    it.freeCompilerArgs += "-Xobjc-generics"
                }
    }

    sourceSets {
        commonMain {
            dependencies {
                api kotlin('stdlib-common')
                api "com.badoo.reaktive:reaktive:${reaktiveVersion}"
                api "com.badoo.reaktive:utils:${reaktiveVersion}"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
                implementation "com.badoo.reaktive:reaktive-testing:${reaktiveVersion}"
            }
        }
        nonNative {
            dependsOn commonMain
        }
        nativeMain {
            dependsOn commonMain
        }
        iosMain.dependsOn(nativeMain)
        macosMain.dependsOn(nativeMain)
        jvmMain {
            dependsOn nonNative
            dependencies {
                api kotlin('stdlib-jdk8')
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        androidMain {
            dependsOn jvmMain
            dependencies {
                api kotlin('stdlib')
            }
        }
        androidTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        jsMain {
            dependsOn nonNative
            dependencies {
                api kotlin('stdlib-js')
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }

        ios {
            dependencies {
            }
        }
        iosTest {
            dependencies {
            }
        }
        macos {
            dependencies {
            }
        }
        macosTest {
            dependencies {
            }
        }
    }
}
